#!/bin/zsh

set -euo pipefail

NVIM_BIN="$HOME/.local/share/shimlink/bin/nvim"
SOCK="$HOME/.config/nvim/nvim.sock"

# Check if invoked as nvimd (daemon mode)
if [[ "${0:t}" == "nvimd" ]]; then
  case "${1:-}" in
    serve)
      # Source zsh configs for full environment
      [[ -f ~/.zshenv ]] && source ~/.zshenv
      [[ -f ~/.zprofile ]] && source ~/.zprofile
      [[ -f ~/.zshrc ]] && source ~/.zshrc

      # Clean up stale socket
      rm -f "$SOCK"

      # Cleanup on exit
      trap "rm -f '$SOCK'" EXIT

      # Start nvim in headless server mode
      export NVIM_SERVER_MODE=1
      exec "$NVIM_BIN" --listen "$SOCK" --headless
      ;;

    reload)
      if [[ "$(uname -s)" == "Darwin" ]]; then
        # On macOS, we need to bootout and bootstrap to reload the plist
        launchctl bootout gui/$(id -u)/com.user.nvim 2>/dev/null || true
        sleep 0.5
        launchctl bootstrap gui/$(id -u) ~/Library/LaunchAgents/com.user.nvim.plist 2>/dev/null || true
        launchctl enable gui/$(id -u)/com.user.nvim
        launchctl kickstart gui/$(id -u)/com.user.nvim
      else
        systemctl --user daemon-reload
      fi
      ;;

    restart)
      if [[ "$(uname -s)" == "Darwin" ]]; then
        launchctl kickstart -k gui/$(id -u)/com.user.nvim
      else
        systemctl --user restart nvim
      fi
      ;;

    cleanup)
      rm -f "$SOCK"
      ;;

    *)
      echo "nvimd: unknown command: ${1:-}"
      echo "Usage: nvimd {serve|reload|restart|cleanup}"
      exit 1
      ;;
  esac
else
  # Client mode: use --server if not already in nvim or in server mode
  if [[ -z "${NVIM_INVIM:-}" && -z "${NVIM_SERVER_MODE:-}" ]]; then
    exec "$NVIM_BIN" --server "$SOCK" "$@"
  else
    exec "$NVIM_BIN" "$@"
  fi
fi
