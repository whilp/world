load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_push")
load("@io_bazel_rules_docker//contrib:test.bzl", "container_test")
load("@io_bazel_rules_docker//python3:image.bzl", "py3_image")
load("@pypi//:requirements.bzl", "requirement")
load("@rules_python//python:defs.bzl", "py_binary")
load("//cmd:labels.bzl", "labels")

MAIN = "bandit.py"

SRCS = [MAIN]

ARGS = [
    "-r",
    "--format=custom",
    "--msg-template={relpath}: {line}: {test_id}: {severity} ({confidence} conf): {msg}",
    ".",
]

DEPS = [
    requirement("bandit"),
    requirement("pbr"),
    requirement("pyyaml"),
    requirement("six"),
    requirement("stevedore"),
    requirement("zipp"),
    requirement("setuptools"),
]

py_binary(
    name = "bandit",
    srcs = SRCS,
    args = ARGS,
    main = MAIN,
    deps = DEPS,
)

py3_image(
    name = "image",
    srcs = SRCS,
    args = ARGS,
    base = ":base",
    main = MAIN,
    deps = DEPS,
)

container_image(
    name = "base",
    base = "@py3_image_base//image",
    labels = labels({"org.opencontainers.image.description": "bandit linter"}),
)

container_push(
    name = "push",
    format = "Docker",
    image = ":image",
    registry = "ghcr.io",
    repository = "whilp/bandit",
    tag = "latest",
)

container_test(
    name = "test",
    configs = ["test.json"],
    image = ":image",
)
